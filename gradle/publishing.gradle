// Additional jars for publishing

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}
    
artifacts {
	archives sourcesJar
}

// MavenPublishing (new)
    
publishing {
        
    repositories {
        maven {
            name "snapshot"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username project.properties['sonatypeUsername'] ?: "nouser"
                password project.properties['sonatypePassword'] ?: "nopass"
            }
        }
        maven {
            name "release"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username project.properties['sonatypeUsername'] ?: "nouser"
                password project.properties['sonatypePassword'] ?: "nopass"
            }
        }
    }
}

// Additional configuration of publishing
apply plugin: ConfigurePublishing

configurePublishing {
    withPomXml {
        asNode().with {
            appendNode('url', 'http://mnlipp.github.io/jgrapes/')
            appendNode('scm').with {
                appendNode('url', 'scm:git@github.com:mnlipp/jgrapes.git')
                appendNode('connection', 'scm:git@github.com:mnlipp/jgrapes.git')
                appendNode('developerConnection', 'git@github.com:mnlipp/jgrapes.git')
            }
            appendNode('licenses').with {
                appendNode('license').with {
                    appendNode('name', 'AGPL 3.0')
                    appendNode('url', 'https://www.gnu.org/licenses/agpl-3.0.en.html')
                    appendNode('distribution', 'repo')
                }
            }
            appendNode('developers').with {
                appendNode('developer').with {
                    appendNode('id', 'mnlipp')
                    appendNode('name', 'Michael N. Lipp')
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.properties['bintrayUser'] ?: "nouser"
    key = project.properties['bintrayApiKey'] ?: "nokey"
    publications = ['maven']
    publish = true
    pkg {
        repo = "jgrapes"
        name = "org.jgrapes:" + project.name
        licenses = ['AGPL-V3']
        vcsUrl = 'https://github.com/mnlipp/jgrapes-portal.git'
        githubRepo = 'mnlipp/jgrapes-portal'
        githubReleaseNotesFile = 'README.md'
        version {
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = project.properties['sonatypeTokenKey'] ?: "nokey"
                password = project.properties['sonatypeTokenSecret'] ?: "nosecret"
                close = '1'
            }
        }
    }
}

afterEvaluate {
    bintray.pkg.version.name = project.version
//    bintrayUpload.dependson "build"
}

task releaseTag {
    doLast {
        String name = project.name.startsWith(project.group + ".") ?
            project.name.substring(project.group.length() + 1) : project.name
        name = name.replace('.', '-')
        String tag = name + "-" + project.version
        grgit.tag.add(name: tag, message: "Released.")
        grgit.push(refsOrSpecs: [tag])
    }
}

build.mustRunAfter "releaseTag"

task releaseToBintray {
    group = "publishing"
    
    dependsOn "releaseTag"
    dependsOn "build"
    dependsOn "bintrayUpload"
}

afterEvaluate {
    if(!project.name.startsWith("org.jgrapes")) {
        tasks.matching { it.group == "publishing" }.all {
            enabled = false
        }
    }
}
